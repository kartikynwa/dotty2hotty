#!/bin/env zsh

maxlen=41

# Check if mpc/mpd is installed
! command -v mpc 1> /dev/null && echo "mpc is not installed." && exit

format_string='[["%title%"][ by %artist%]]|[%file%]'

# Store mpc output
output=("${(@f)$(mpc -f "${format_string}")}")

# Exit if mpd is not running
[[ -z "$output" ]] && echo "mpd down" && exit

# Exit if queue is empty
[[ "${#output[@]}" -eq 1 ]] && echo "mpd queue empty" && exit

# Check if ashuffle is running
pgrep -x ashuffle > /dev/null 2>&1 && ashuffle=" <ash>"

# Check if tags are available, otherwise show only filename
song_info="${output[1]}"

# Splice output if it is too long
[[ ${#song_info} -ge ${maxlen} ]] && song_info="${song_info:0:$maxlen - 3}â€¦"

# Show `|` if paused, `>` if playing
status_line="${output[2]}"
[[ "${status_line%]*}" = "[playing" ]] && symbol=">" || symbol="|"

# Get mpd's volume
vol="${output[3]}" && vol="${${${vol#*:}%\%*}// /}"

# Echo the crap of out it
[[ -n "$song_info" ]] && echo "${symbol} ${song_info} <${vol}%>${ashuffle}"
